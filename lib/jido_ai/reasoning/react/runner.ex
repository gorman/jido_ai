defmodule Jido.AI.Reasoning.ReAct.Runner do
  @moduledoc """
  Task-based ReAct runner.

  Produces a lazy event stream via `Stream.resource/3` and does not persist runtime
  state outside of caller-owned checkpoint tokens.
  """

  alias Jido.AI.Output
  alias Jido.AI.PendingInputServer
  alias Jido.AI.Query
  alias Jido.AI.Reasoning.ReAct.{Config, Event, PendingToolCall, State, StreamResume, Token, ToolSelection}
  alias Jido.AI.Effects
  alias Jido.AI.Context, as: AIContext
  alias Jido.AI.Error
  alias Jido.AI.Turn
  alias Jido.Agent.State, as: AgentState

  require Logger

  @cleanup_wait_ms 25
  @stream_control_wait_ms 10
  @openai_websocket_session_key {__MODULE__, :openai_websocket_session}

  # Injected as a user message when the agent repeats the exact same tool
  # calls with identical arguments on consecutive iterations.
  @cycle_warning "You already called the same tool(s) with identical parameters in the previous iteration and got the same results. Do NOT repeat the same calls. Either use the results you already have to form a final answer, or try a different approach."

  @type stream_opt ::
          {:request_id, String.t()}
          | {:run_id, String.t()}
          | {:state, State.t()}
          | {:task_supervisor, pid() | atom()}
          | {:context, map()}

  @doc """
  Starts a new ReAct coordinator task and returns a lazy event stream.
  """
  @spec stream(String.t(), Config.t(), [stream_opt()]) :: Enumerable.t()
  def stream(query, %Config{} = config, opts \\ []) when is_binary(query) or is_list(query) do
    initial_state =
      case Keyword.get(opts, :state) do
        %State{} = state -> state
        _ -> State.new(query, config.system_prompt, request_id_opts(opts))
      end

    build_stream(initial_state, config, Keyword.put(opts, :query, query), emit_start?: true)
  end

  @doc """
  Continues a ReAct run from an existing runtime state.
  """
  @spec stream_from_state(State.t(), Config.t(), [stream_opt()]) :: Enumerable.t()
  def stream_from_state(%State{} = state, %Config{} = config, opts \\ []) do
    query = Keyword.get(opts, :query)

    state =
      case query do
        q when is_binary(q) and q != "" -> append_query(state, q)
        q when is_list(q) -> append_query(state, q)
        _ -> state
      end

    build_stream(state, config, opts, emit_start?: false)
  end

  defp build_stream(%State{} = initial_state, %Config{} = config, opts, stream_opts) do
    owner = self()
    ref = make_ref()
    receive_timeout_ms = Config.stream_timeout(config)

    case start_task(fn -> coordinator(owner, ref, initial_state, config, opts, stream_opts) end, opts) do
      {:ok, pid} ->
        monitor_ref = Process.monitor(pid)

        Stream.resource(
          fn ->
            %{
              done?: false,
              down?: false,
              cancel_sent?: false,
              pid: pid,
              monitor_ref: monitor_ref,
              receive_timeout_ms: receive_timeout_ms,
              stream_cancel: nil,
              stream_cancelled?: false,
              ref: ref
            }
          end,
          &next_event(owner, &1),
          &cleanup(owner, &1)
        )

      {:error, reason} ->
        Stream.map([reason], fn error ->
          raise "Failed to start ReAct runner task: #{inspect(error)}"
        end)
    end
  end

  defp coordinator(owner, ref, state, config, opts, stream_opts) do
    context = build_runtime_context(Keyword.get(opts, :context, %{}), state, config)

    state =
      case stream_opts[:emit_start?] do
        true ->
          {state, _} =
            emit_event(state, owner, ref, :request_started, %{
              query: latest_query(state),
              config_fingerprint: Config.fingerprint(config)
            })

          state

        _ ->
          state
      end

    try do
      state
      |> run_loop(owner, ref, config, context)
      |> finalize(owner, ref, config)
    catch
      {:cancelled, %State{} = current_state, reason} ->
        seal_pending_input_server(config)

        cancelled_state =
          current_state
          |> State.put_status(:cancelled)
          |> State.put_result("Request cancelled (reason: #{inspect(reason)})")

        {cancelled_state, _} =
          emit_event(cancelled_state, owner, ref, :request_cancelled, %{reason: reason})

        {_cancelled_state, _token} = emit_checkpoint(cancelled_state, owner, ref, config, :terminal)
        send(owner, {:react_runner, ref, :done})

      kind, reason ->
        seal_pending_input_server(config)

        failed_state =
          state
          |> State.put_status(:failed)
          |> State.put_error(%{kind: kind, reason: inspect(reason)})

        {failed_state, _} =
          emit_event(failed_state, owner, ref, :request_failed, %{
            error: %{kind: kind, reason: inspect(reason)},
            error_type: :runtime
          })

        {_failed_state, _token} = emit_checkpoint(failed_state, owner, ref, config, :terminal)
        send(owner, {:react_runner, ref, :done})
    after
      close_openai_websocket_session()
    end
  end

  defp run_loop(%State{} = state, owner, ref, %Config{} = config, context) do
    check_cancel!(state, ref)

    cond do
      state.status in [:completed, :failed, :cancelled] ->
        state

      state.status == :awaiting_tools and state.pending_tool_calls != [] ->
        case run_pending_tool_round(state, owner, ref, config, context) do
          {:ok, state, context} ->
            run_loop(state, owner, ref, config, context)

          {:terminal, state, context} ->
            settle_final_answer(state, owner, ref, config, context)

          {:error, state, reason, error_type} ->
            fail_run(state, owner, ref, config, reason, error_type)
        end

      state.iteration > config.max_iterations ->
        seal_pending_input_server(config)

        completed =
          state
          |> State.put_status(:completed)
          |> State.put_result("Maximum iterations reached without a final answer.")

        case complete_run(completed, owner, ref, config, :max_iterations) do
          {:ok, completed} -> completed
          {:error, failed_state, reason, error_type} -> fail_run(failed_state, owner, ref, config, reason, error_type)
        end

      true ->
        case drain_pending_input(state, owner, ref, config) do
          {:ok, state} ->
            case run_llm_step(state, owner, ref, config, context) do
              {:final_answer, state} ->
                settle_final_answer(state, owner, ref, config, context)

              # The provider paused the turn; the paused assistant content is
              # already appended, so loop straight into the next LLM call.
              {:paused, state} ->
                run_loop(state, owner, ref, config, context)

              # The stream died in transport and the turn is being resumed;
              # whatever survived is already appended, so ask again.
              {:resume, state} ->
                run_loop(state, owner, ref, config, context)

              {:tool_calls, state, tool_calls} ->
                prev_signature = Map.get(state, :__prev_tool_signature__)
                current_signature = tool_call_signature(tool_calls)

                case run_tool_round(state, owner, ref, config, context, tool_calls) do
                  {:ok, state, context} ->
                    state = Map.put(state, :__prev_tool_signature__, current_signature)

                    state =
                      if prev_signature == current_signature and prev_signature != nil do
                        corrected_context = AIContext.append_user(state.context, @cycle_warning)
                        %{state | context: corrected_context}
                      else
                        state
                      end

                    run_loop(state, owner, ref, config, context)

                  {:terminal, state, context} ->
                    settle_final_answer(state, owner, ref, config, context)

                  {:error, state, reason, error_type} ->
                    fail_run(state, owner, ref, config, reason, error_type)
                end

              {:error, state, reason, error_type} ->
                fail_run(state, owner, ref, config, reason, error_type)
            end

          {:error, state, reason} ->
            fail_run(state, owner, ref, config, {:pending_input_server, reason}, :runtime)
        end
    end
  end

  defp settle_final_answer(%State{} = state, owner, ref, %Config{} = config, context) do
    case maybe_continue_after_final_answer(state, owner, ref, config) do
      {:continue, state} ->
        run_loop(state, owner, ref, config, context)

      {:complete, state} ->
        state

      {:error, state, reason, error_type} ->
        fail_run(state, owner, ref, config, reason, error_type)
    end
  end

  defp run_llm_step(%State{} = state, owner, ref, %Config{} = config, runtime_context) do
    check_cancel!(state, ref)

    call_id = "call_#{state.run_id}_#{state.iteration}_#{Jido.Util.generate_id()}"

    state =
      state
      |> State.clear_streaming()
      |> State.put_llm_call_id(call_id)

    with {:ok, request} <- build_turn_request(state, config, runtime_context) do
      state = %{state | active_tools: request.tools}

      {state, _} =
        emit_event(
          state,
          owner,
          ref,
          :llm_started,
          %{
            call_id: call_id,
            model: Jido.AI.model_label(request.model),
            message_count: length(request.messages)
          },
          llm_call_id: call_id
        )

      case request_turn(state, owner, ref, config, request.messages, request.llm_opts, request.model) do
        {:ok, state, turn, response_id} ->
          # A response that ran code execution reports its sandbox container,
          # and every later request in the run must carry the id while work is
          # still in flight — resuming a pause_turn, or returning results for
          # tool uses the sandbox generated programmatically — or the API
          # rejects the request ("container_id is required"). A final answer
          # clears it: containers expire, so the id must not leak into later
          # runs.
          container_id =
            cond do
              is_binary(turn.container_id) -> turn.container_id
              Turn.paused?(turn) or Turn.needs_tools?(turn) -> state.container_id
              true -> nil
            end

          state =
            state
            |> State.merge_usage(turn.usage)
            |> State.put_llm_response_id(response_id)
            |> State.put_container_id(container_id)

          # A paused turn (Turn.paused?/1) is neither an answer nor a failure,
          # and often has blank text — it must be handled before terminal
          # validation would reject it.
          if Turn.paused?(turn) do
            handle_paused_turn(state, owner, ref, config, turn, call_id)
          else
            run_llm_outcome(state, owner, ref, config, turn, call_id)
          end

        {:resume, state, partial_turn} ->
          handle_stream_resume(state, owner, ref, config, partial_turn)

        {:error, state, reason, error_type} ->
          {:error, state, reason, error_type}
      end
    else
      {:error, reason} ->
        {:error, state, reason, :request_transform}
    end
  end

  defp run_llm_outcome(%State{} = state, owner, ref, %Config{} = config, turn, call_id) do
    case validate_terminal_response(turn) do
      :ok ->
        {state, _} =
          emit_event(
            state,
            owner,
            ref,
            :llm_completed,
            %{
              call_id: call_id,
              model: turn.model,
              turn_type: turn.type,
              text: turn.text,
              thinking_content: turn.thinking_content,
              reasoning_details: Map.get(turn, :reasoning_details),
              content_parts: provider_content_parts(turn),
              tool_calls: turn.tool_calls,
              usage: turn.usage,
              finish_reason: turn.finish_reason
            },
            llm_call_id: call_id
          )

        state =
          AIContext.append_assistant(
            state.context,
            turn.text,
            case turn.type do
              :tool_calls -> turn.tool_calls
              _ -> nil
            end,
            assistant_context_opts(turn) ++ [content_parts: provider_content_parts(turn)]
          )
          |> then(&%{state | context: &1})

        {state, _token} = emit_checkpoint(state, owner, ref, config, :after_llm)

        case Turn.needs_tools?(turn) do
          true ->
            # Carry this round's prose forward as the provisional result: a
            # terminal tool can end the run without a further LLM response,
            # and this is then the answer the model actually gave. A round
            # that keeps looping overwrites it.
            awaiting =
              state
              |> State.put_result(turn.text)
              |> State.put_status(:awaiting_tools)

            {:tool_calls, awaiting, turn.tool_calls}

          _ ->
            completed =
              state
              |> State.put_status(:completed)
              |> State.put_result(turn.text)

            {:final_answer, completed}
        end

      {:error, reason} ->
        {:error, state, reason, :llm_response}
    end
  end

  # The provider paused the turn mid-execution (Anthropic's pause_turn during
  # server-tool use). Append the paused assistant content — including the
  # provider-native blocks the resume depends on — and loop for another LLM
  # call with no new user or tool message, so the model continues its own
  # turn. Counts against max_iterations like any other iteration.
  defp handle_paused_turn(%State{} = state, owner, ref, %Config{} = config, turn, call_id) do
    {state, _} =
      emit_event(
        state,
        owner,
        ref,
        :llm_completed,
        %{
          call_id: call_id,
          model: turn.model,
          turn_type: turn.type,
          text: turn.text,
          thinking_content: turn.thinking_content,
          reasoning_details: Map.get(turn, :reasoning_details),
          content_parts: turn.content_parts,
          tool_calls: turn.tool_calls,
          usage: turn.usage,
          finish_reason: turn.finish_reason
        },
        llm_call_id: call_id
      )

    context_opts = assistant_context_opts(turn) ++ [content_parts: turn.content_parts]

    state =
      state.context
      |> AIContext.append_assistant(turn.text, nil, context_opts)
      |> then(&%{state | context: &1})
      |> State.inc_iteration()

    {state, _token} = emit_checkpoint(state, owner, ref, config, :after_llm)

    {:paused, state}
  end

  # The stream died in transport and the run is resuming the turn. This is the
  # same request shape as a pause_turn continuation: the partial assistant
  # content — provider-native blocks in the order they arrived, cut at the last
  # completed tool result — goes back with the container id and no new user or
  # tool message, so the model carries on from the boundary. No `llm_completed`
  # event: the turn did not complete, and the content going back is a fragment
  # the model is about to extend.
  defp handle_stream_resume(%State{} = state, owner, ref, %Config{} = config, %Turn{} = turn) do
    context_opts = assistant_context_opts(turn) ++ [content_parts: turn.content_parts]

    state =
      state.context
      |> AIContext.append_assistant(turn.text, nil, context_opts)
      |> then(&%{state | context: &1})
      |> State.put_container_id(resumed_container_id(turn, state))
      |> State.inc_iteration()

    {state, _token} = emit_checkpoint(state, owner, ref, config, :after_llm)

    {:resume, state}
  end

  # Nothing completed before the connection died, so there is no partial turn to
  # replay: the next request repeats this turn with the context untouched.
  defp handle_stream_resume(%State{} = state, owner, ref, %Config{} = config, nil) do
    state = State.inc_iteration(state)
    {state, _token} = emit_checkpoint(state, owner, ref, config, :after_llm)

    {:resume, state}
  end

  defp build_turn_request(%State{} = state, %Config{} = config, runtime_context) do
    base_request = %{
      messages: AIContext.to_messages(state.context),
      llm_opts:
        config
        |> Config.llm_opts()
        |> maybe_put_previous_response_id(state.llm_response_id)
        |> maybe_put_container(state.container_id),
      tools: config.tools,
      model: config.model
    }

    with {:ok, request} <- maybe_transform_request(base_request, state, config, runtime_context),
         request <- maybe_apply_output_instructions(request, config.output),
         {:ok, messages} <- normalize_request_messages(request),
         {:ok, tools} <- normalize_request_tools(request),
         effective_model <- Map.get(request, :model, config.model),
         llm_opts <- sync_tools_in_llm_opts(config, request.llm_opts, tools),
         llm_opts <- maybe_put_openai_websocket_session(effective_model, llm_opts) do
      {:ok,
       %{
         messages: messages,
         llm_opts: llm_opts,
         tools: tools,
         model: effective_model
       }}
    end
  end

  defp maybe_transform_request(request, _state, %Config{request_transformer: nil}, _runtime_context), do: {:ok, request}

  defp maybe_transform_request(request, %State{} = state, %Config{} = config, runtime_context) do
    module = config.request_transformer

    case module.transform_request(request, state, config, runtime_context) do
      {:ok, %{} = overrides} ->
        {:ok, apply_request_overrides(request, overrides, config)}

      {:error, reason} ->
        {:error, {:request_transformer, reason}}

      other ->
        {:error, {:invalid_request_transformer_result, other}}
    end
  rescue
    e ->
      {:error, {:request_transformer_exception, %{error: Exception.message(e), type: e.__struct__}}}
  end

  defp apply_request_overrides(request, overrides, %Config{} = config) when is_map(request) and is_map(overrides) do
    # Resolve `:model` first so any per-turn provider swap is in place before
    # we re-validate llm_opts overrides — `provider_options` keys differ per
    # provider, so xAI-only keys (e.g. `xai_api`) must validate against xAI's
    # schema, not the compile-time provider's schema.
    request = maybe_put_request_model(request, Map.get(overrides, :model, Map.get(overrides, "model")))

    request
    |> maybe_put_request_field(:messages, Map.get(overrides, :messages, Map.get(overrides, "messages")))
    |> maybe_put_request_field(:tools, Map.get(overrides, :tools, Map.get(overrides, "tools")))
    |> maybe_merge_request_llm_opts(config, Map.get(overrides, :llm_opts, Map.get(overrides, "llm_opts")))
  end

  defp maybe_put_request_field(request, _field, nil), do: request
  defp maybe_put_request_field(request, field, value), do: Map.put(request, field, value)

  defp maybe_put_request_model(request, nil), do: request

  defp maybe_put_request_model(request, model_override) do
    Map.put(request, :model, Jido.AI.resolve_model(model_override))
  end

  defp maybe_merge_request_llm_opts(request, _config, nil), do: request

  defp maybe_merge_request_llm_opts(request, %Config{} = config, llm_opts_override) do
    # If a per-turn `:model` override was applied earlier in apply_request_overrides,
    # validate the llm_opts overrides against THAT provider's schema. Otherwise
    # fall back to the compile-time `config.model` schema (existing behavior).
    effective_model = Map.get(request, :model, config.model)

    Map.update!(request, :llm_opts, fn base_opts ->
      Config.merge_llm_opts(config, base_opts, llm_opts_override, effective_model)
    end)
  end

  defp maybe_apply_output_instructions(%{messages: messages} = request, %Output{} = output) when is_list(messages) do
    %{request | messages: Output.apply_instructions(messages, output)}
  end

  defp maybe_apply_output_instructions(request, _output), do: request

  defp normalize_request_messages(%{messages: messages}) when is_list(messages), do: {:ok, messages}
  defp normalize_request_messages(_request), do: {:error, :invalid_request_messages}

  defp normalize_request_tools(%{tools: tools}) do
    case ToolSelection.normalize_input(tools) do
      {:ok, action_map} -> {:ok, action_map}
      {:error, reason} -> {:error, {:invalid_request_tools, reason}}
    end
  end

  defp normalize_request_tools(_request), do: {:error, :invalid_request_tools}

  defp sync_tools_in_llm_opts(%Config{} = config, llm_opts, tools) when is_list(llm_opts) and is_map(tools) do
    reqllm_tools = Config.reqllm_tools(%{config | tools: tools})

    llm_opts
    |> Keyword.delete(:tools)
    |> Keyword.put(:tools, reqllm_tools)
  end

  defp request_turn(%State{} = state, owner, ref, %Config{} = config, messages, llm_opts, model) do
    case config.streaming do
      false ->
        request_turn_generate(state, config, messages, llm_opts, model)

      _ ->
        request_turn_stream(state, owner, ref, config, messages, llm_opts, model)
    end
  end

  defp request_turn_stream(%State{} = state, owner, ref, %Config{} = config, messages, llm_opts, model) do
    case ReqLLM.Generation.stream_text(model, messages, llm_opts) do
      {:ok, stream_response} ->
        announce_stream_control(owner, ref, stream_response)

        case consume_stream(state, owner, ref, config, stream_response, model) do
          {:ok, updated_state, turn, response_id} -> {:ok, updated_state, turn, response_id}
          {:resume, updated_state, partial_turn} -> {:resume, updated_state, partial_turn}
          {:error, updated_state, reason} -> {:error, updated_state, reason, :llm_stream}
        end

      {:error, reason} ->
        {:error, state, reason, :llm_request}
    end
  end

  defp request_turn_generate(%State{} = state, %Config{} = config, messages, llm_opts, model) do
    case ReqLLM.Generation.generate_text(model, messages, llm_opts) do
      {:ok, response} ->
        consume_generate(state, config, response, model)

      {:error, reason} ->
        {:error, state, reason, :llm_request}
    end
  end

  defp consume_stream(%State{} = state, owner, ref, %Config{} = config, stream_response, model) do
    check_cancel!(state, ref)

    trace_cfg = config.trace
    state_key = stream_state_key(ref)
    heartbeat_interval_ms = progress_interval_ms(config)
    started_ms = monotonic_ms()

    Process.put(state_key, state)
    Process.put(stream_signal_key(ref), started_ms)
    arm_chunk_capture(ref, state, config)

    case ReqLLM.StreamResponse.process_stream(
           stream_response,
           stream_process_opts(owner, ref, trace_cfg, state_key, heartbeat_interval_ms, model)
         ) do
      {:ok, response} ->
        current_state = current_stream_state(state_key, state)

        turn =
          response
          |> Turn.from_response(model: Jido.AI.model_label(model))
          |> apply_stream_accumulator(current_state)

        {:ok, current_state, turn, extract_response_id(response)}

      {:error, reason} ->
        current_stream_state(state_key, state)
        |> maybe_resume_stream(config, reason, %{
          ref: ref,
          model: model,
          stream_response: stream_response,
          started_ms: started_ms
        })
    end
  rescue
    e ->
      {:error, current_stream_state(stream_state_key(ref), state), %{error: Exception.message(e), type: e.__struct__}}
  after
    Process.delete(stream_state_key(ref))
    Process.delete(stream_signal_key(ref))
    Process.delete(stream_activity_key(ref))
    Process.delete(stream_chunks_key(ref))
  end

  # The chunks the stream has already delivered, kept caller-side so they
  # survive the mid-stream raise that unwinds the provider's own accumulator.
  # Only armed while a resume is still available, since a long turn holds every
  # chunk it produced.
  defp arm_chunk_capture(ref, %State{} = state, %Config{} = config) do
    if state.stream_resumes < config.max_stream_resumes do
      Process.put(stream_chunks_key(ref), [])
    end

    :ok
  end

  defp capture_stream_chunk(chunks_key, chunk) do
    case Process.get(chunks_key) do
      chunks when is_list(chunks) -> Process.put(chunks_key, [chunk | chunks])
      _ -> nil
    end

    :ok
  end

  defp captured_chunks(ref) do
    case Process.get(stream_chunks_key(ref)) do
      chunks when is_list(chunks) -> Enum.reverse(chunks)
      _ -> []
    end
  end

  # A stream killed in transport says nothing about the turn: the provider kept
  # working, and a code-execution container outlives the connection. Replaying
  # the part of the assistant message that made it across — truncated to its
  # last completed tool result — continues the turn instead of failing the run.
  defp maybe_resume_stream(%State{} = state, %Config{} = config, reason, ctx) do
    if state.stream_resumes < config.max_stream_resumes and StreamResume.resumable_error?(reason) do
      resume_stream(state, ctx)
    else
      {:error, state, reason}
    end
  end

  defp resume_stream(%State{} = state, ctx) do
    state = State.inc_stream_resumes(state)
    chunks = captured_chunks(ctx.ref)

    case StreamResume.partial_turn(chunks, ctx.stream_response, Jido.AI.model_label(ctx.model)) do
      {:ok, turn, blocks_kept} ->
        log_stream_resume(state, ctx, blocks_kept, resumed_container_id(turn, state))
        {:resume, state, turn}

      :none ->
        log_stream_resume(state, ctx, 0, state.container_id)
        {:resume, state, nil}
    end
  end

  defp resumed_container_id(%Turn{container_id: container_id}, _state) when is_binary(container_id),
    do: container_id

  defp resumed_container_id(_turn, %State{} = state), do: state.container_id

  defp log_stream_resume(%State{} = state, ctx, blocks_kept, container_id) do
    Logger.info(
      "react stream_resume: resume=#{state.stream_resumes} run_id=#{state.run_id} " <>
        "stream_seconds=#{div(monotonic_ms() - ctx.started_ms, 1000)} blocks_kept=#{blocks_kept} " <>
        "container=#{is_binary(container_id)}"
    )
  end

  defp consume_generate(%State{} = state, %Config{} = _config, response, model) do
    turn = Turn.from_response(response, model: Jido.AI.model_label(model))
    {:ok, state, turn, extract_response_id(response)}
  rescue
    e ->
      {:error, state, %{error: Exception.message(e), type: e.__struct__}, :llm_response}
  end

  defp extract_response_id(%ReqLLM.Response{message: %ReqLLM.Message{metadata: metadata}})
       when is_map(metadata) do
    metadata[:response_id]
  end

  defp extract_response_id(%{message: %{metadata: metadata}}) when is_map(metadata) do
    metadata[:response_id]
  end

  defp extract_response_id(_), do: nil

  defp maybe_put_previous_response_id(llm_opts, nil), do: llm_opts

  defp maybe_put_previous_response_id(llm_opts, response_id) when is_list(llm_opts) and is_binary(response_id) do
    provider_options = llm_opts |> Keyword.get(:provider_options, []) |> normalize_provider_options()
    Keyword.put(llm_opts, :provider_options, Keyword.put(provider_options, :previous_response_id, response_id))
  end

  defp maybe_put_previous_response_id(llm_opts, _response_id), do: llm_opts

  # Anthropic-specific by necessity: pause_turn resume is an Anthropic
  # protocol feature (like Turn.paused?/1), and other providers never set a
  # container id, so the option is inert everywhere else.
  defp maybe_put_container(llm_opts, container_id)
       when is_list(llm_opts) and is_binary(container_id) do
    Keyword.put(llm_opts, :anthropic_container, container_id)
  end

  defp maybe_put_container(llm_opts, _container_id), do: llm_opts

  defp normalize_provider_options(options) when is_list(options), do: Enum.reject(options, fn {k, _v} -> is_nil(k) end)

  defp normalize_provider_options(options) when is_map(options) do
    options
    |> Enum.reject(fn {k, _v} -> is_nil(k) end)
    |> Enum.flat_map(fn
      {k, v} when is_atom(k) -> [{k, v}]
      {"previous_response_id", v} -> [previous_response_id: v]
      _ -> []
    end)
  end

  defp normalize_provider_options(_options), do: []

  defp maybe_put_openai_websocket_session(model, llm_opts) when is_list(llm_opts) do
    provider_options = llm_opts |> Keyword.get(:provider_options, []) |> normalize_provider_options()

    if reuse_openai_websocket?(model, provider_options) and is_nil(provider_options[:openai_websocket_session]) do
      case openai_websocket_session(model, llm_opts) do
        session when is_pid(session) ->
          Keyword.put(llm_opts, :provider_options, Keyword.put(provider_options, :openai_websocket_session, session))

        _ ->
          llm_opts
      end
    else
      llm_opts
    end
  end

  defp reuse_openai_websocket?(model, provider_options) do
    openai_responses_websocket_model?(model) and truthy?(provider_options[:openai_reuse_websocket]) and
      provider_options[:openai_stream_transport] in [:websocket, "websocket"]
  end

  defp openai_responses_websocket_model?(model) when is_map(model) do
    Map.get(model, :provider) in [:openai, :openai_codex]
  end

  defp openai_responses_websocket_model?({provider, _model_id, _provider_opts})
       when provider in [:openai, :openai_codex],
       do: true

  defp openai_responses_websocket_model?({provider, _provider_opts}) when provider in [:openai, :openai_codex],
    do: true

  defp openai_responses_websocket_model?(model) when is_binary(model) do
    String.starts_with?(model, ["openai:", "openai_codex:"])
  end

  defp openai_responses_websocket_model?(_model), do: false

  defp truthy?(value), do: value in [true, "true", 1, "1"]

  defp openai_websocket_session(model, llm_opts) do
    case Process.get(@openai_websocket_session_key) do
      session when is_pid(session) ->
        session

      _ ->
        start_openai_websocket_session(model, llm_opts)
    end
  end

  defp start_openai_websocket_session(model, llm_opts) do
    module = openai_websocket_session_module(model)

    with {:ok, resolved_model} <- ReqLLM.model(model),
         true <- Code.ensure_loaded?(module),
         true <- function_exported?(module, :start_responses_session, 2) do
      case apply(module, :start_responses_session, [resolved_model, llm_opts]) do
        {:ok, session} ->
          Process.put(@openai_websocket_session_key, session)
          session

        _ ->
          nil
      end
    end
  end

  defp openai_websocket_session_module(%{provider: :openai_codex}), do: ReqLLM.Providers.OpenAICodex
  defp openai_websocket_session_module({:openai_codex, _model_id, _provider_opts}), do: ReqLLM.Providers.OpenAICodex
  defp openai_websocket_session_module({:openai_codex, _provider_opts}), do: ReqLLM.Providers.OpenAICodex
  defp openai_websocket_session_module("openai_codex:" <> _model), do: ReqLLM.Providers.OpenAICodex
  defp openai_websocket_session_module(_model), do: ReqLLM.Providers.OpenAI

  defp close_openai_websocket_session do
    case Process.delete(@openai_websocket_session_key) do
      session when is_pid(session) ->
        close_websocket_session(session)

      _ ->
        :ok
    end
  end

  defp close_websocket_session(session) do
    if Process.alive?(session), do: ReqLLM.Streaming.WebSocketSession.close(session)
  catch
    _, _ -> :ok
  end

  defp run_tool_round(%State{} = state, owner, ref, %Config{} = config, context, tool_calls)
       when is_list(tool_calls) do
    case preflight_tool_calls(tool_calls, context) do
      :ok ->
        effective_tools =
          case state.active_tools do
            %{} = tools when map_size(tools) > 0 -> tools
            _ -> config.tools
          end

        tool_config = %{config | tools: effective_tools}
        pending = Enum.map(tool_calls, &PendingToolCall.from_tool_call/1)
        state = State.put_pending_tools(state, pending)

        {state, _} =
          Enum.reduce(pending, {state, nil}, fn pending_call, {acc, _} ->
            emit_event(
              acc,
              owner,
              ref,
              :tool_started,
              %{
                tool_call_id: pending_call.id,
                tool_name: pending_call.name,
                arguments: maybe_redact_args(pending_call.arguments, config)
              },
              tool_call_id: pending_call.id,
              tool_name: pending_call.name
            )
          end)

        results =
          pending
          |> Task.async_stream(
            fn call -> execute_tool_with_retries(call, tool_config, context) end,
            ordered: true,
            max_concurrency: tool_config.tool_exec.concurrency,
            timeout: tool_config.tool_exec.timeout_ms + 50
          )
          |> Enum.map(fn
            {:ok, result} -> result
            {:exit, reason} -> {:error, %{type: :task_exit, reason: inspect(reason)}}
          end)

        {state, updated_context} =
          Enum.reduce(results, {state, state.context}, fn
            {pending_call, result, attempts, duration_ms}, {acc, context_acc} ->
              completed = PendingToolCall.complete(pending_call, result, attempts, duration_ms)

              {acc, _} =
                emit_event(
                  acc,
                  owner,
                  ref,
                  :tool_completed,
                  %{
                    tool_call_id: completed.id,
                    tool_name: completed.name,
                    result: result,
                    attempts: attempts,
                    duration_ms: duration_ms
                  },
                  tool_call_id: completed.id,
                  tool_name: completed.name
                )

              content = Turn.format_tool_result_content(result)
              context_acc = AIContext.append_tool_result(context_acc, completed.id, completed.name, content)
              {acc, context_acc}

            {:error, reason}, {acc, context_acc} ->
              Logger.error("tool task failure", reason: inspect(reason))
              {acc, context_acc}
          end)

        state =
          state
          |> State.put_status(:running)
          |> State.clear_pending_tools()
          |> State.inc_iteration()
          |> Map.put(:context, updated_context)

        {state, _token} = emit_checkpoint(state, owner, ref, config, :after_tools)
        next_context = evolve_context_state_snapshot(context, results)

        # Terminal tools are checked only once the whole round has run, so a
        # round that pairs one with other calls still executes all of them and
        # every result reaches the event stream before the run completes.
        if terminal_tool_succeeded?(results, config) do
          {:terminal, State.put_status(state, :completed), next_context}
        else
          {:ok, state, next_context}
        end

      {:error, reason} ->
        {:error, State.put_status(state, :failed), reason, :tool_guardrail}
    end
  end

  # A failed terminal tool must not end the turn — the model still has to see the
  # error and react to it.
  defp terminal_tool_succeeded?(results, %Config{} = config) do
    Enum.any?(results, fn
      {%PendingToolCall{name: name}, {:ok, _value, _effects}, _attempts, _duration_ms} ->
        Config.terminal_tool?(config, name)

      {%PendingToolCall{name: name}, {:ok, _value}, _attempts, _duration_ms} ->
        Config.terminal_tool?(config, name)

      _other ->
        false
    end)
  end

  defp run_pending_tool_round(%State{} = state, owner, ref, %Config{} = config, context) do
    run_tool_round(
      State.put_status(state, :awaiting_tools),
      owner,
      ref,
      config,
      context,
      Enum.map(state.pending_tool_calls, fn
        %PendingToolCall{} = call -> %{id: call.id, name: call.name, arguments: call.arguments}
        %{} = call -> call
      end)
    )
  end

  defp preflight_tool_calls(tool_calls, context) when is_list(tool_calls) and is_map(context) do
    Enum.reduce_while(tool_calls, :ok, fn tool_call, :ok ->
      case maybe_apply_tool_guardrail_callback(
             %{
               tool_name: tool_call_field(tool_call, :name, ""),
               tool_call_id: tool_call_field(tool_call, :id, ""),
               arguments: tool_call_field(tool_call, :arguments, %{}),
               context: context
             },
             context
           ) do
        :ok ->
          {:cont, :ok}

        {:error, reason} ->
          {:halt, {:error, reason}}

        {:interrupt, interrupt} ->
          {:halt, {:error, {:interrupt, interrupt}}}
      end
    end)
  end

  defp preflight_tool_calls(_tool_calls, _context), do: :ok

  defp tool_call_field(tool_call, key, default) when is_map(tool_call) do
    Map.get(tool_call, key, Map.get(tool_call, Atom.to_string(key), default))
  end

  defp maybe_apply_tool_guardrail_callback(tool_call, context) when is_map(context) do
    callback = context[:__tool_guardrail_callback__]

    case callback do
      fun when is_function(fun, 1) -> fun.(tool_call)
      _ -> :ok
    end
  rescue
    error ->
      {:error, {:tool_guardrail_callback_failed, Exception.message(error)}}
  end

  defp evolve_context_state_snapshot(context, results) when is_map(context) and is_list(results) do
    case current_state_snapshot(context) do
      {:ok, snapshot} ->
        updated_snapshot =
          Enum.reduce(results, snapshot, fn
            {_pending_call, result, _attempts, _duration_ms}, acc ->
              apply_state_effects(acc, result)

            {:error, _reason}, acc ->
              acc

            _other, acc ->
              acc
          end)

        Map.put(context, :state, updated_snapshot)

      :error ->
        context
    end
  end

  defp evolve_context_state_snapshot(context, _results), do: context

  defp current_state_snapshot(context) when is_map(context) do
    case context[:state] do
      %{} = snapshot -> {:ok, snapshot}
      _ -> :error
    end
  end

  defp build_runtime_context(context, %State{} = state, %Config{} = config) when is_map(context) do
    context
    |> Map.put_new(:request_id, state.request_id)
    |> Map.put_new(:run_id, state.run_id)
    |> Map.put_new(:effect_policy, config.effect_policy)
    |> Map.put_new(:observability, config.observability)
  end

  defp build_runtime_context(_context, %State{} = state, %Config{} = config) do
    build_runtime_context(%{}, state, config)
  end

  defp apply_state_effects(snapshot, result) when is_map(snapshot) do
    {_status, _payload, effects} = Effects.normalize_result(result)

    Enum.reduce(effects, snapshot, fn
      %Jido.Agent.StateOp.SetState{attrs: attrs}, acc when is_map(attrs) ->
        AgentState.merge(acc, attrs)

      %Jido.Agent.StateOp.ReplaceState{state: new_state}, _acc when is_map(new_state) ->
        new_state

      %Jido.Agent.StateOp.DeleteKeys{keys: keys}, acc when is_list(keys) ->
        Map.drop(acc, keys)

      %Jido.Agent.StateOp.SetPath{path: path, value: value}, acc when is_list(path) ->
        put_in_path(acc, path, value)

      %Jido.Agent.StateOp.DeletePath{path: path}, acc when is_list(path) ->
        delete_in_path(acc, path)

      _other, acc ->
        acc
    end)
  end

  defp apply_state_effects(snapshot, _result), do: snapshot

  defp put_in_path(map, [key], value) when is_map(map), do: Map.put(map, key, value)

  defp put_in_path(map, [key | rest], value) when is_map(map) do
    nested =
      case Map.get(map, key) do
        %{} = current -> current
        _ -> %{}
      end

    Map.put(map, key, put_in_path(nested, rest, value))
  end

  defp put_in_path(map, _path, _value), do: map

  defp delete_in_path(map, [key]) when is_map(map), do: Map.delete(map, key)

  defp delete_in_path(map, [key | rest]) when is_map(map) do
    case Map.get(map, key) do
      %{} = nested ->
        Map.put(map, key, delete_in_path(nested, rest))

      _ ->
        map
    end
  end

  defp delete_in_path(map, _path), do: map

  # Never execute a call whose arguments were cut off in transport — the
  # empty fallback args would silently do the wrong thing (imagine a
  # mutation running with all-default arguments). The error tool_result
  # tells the model to re-issue the call, which also satisfies the API
  # requirement that every tool_use id gets a result.
  defp execute_tool_with_retries(%PendingToolCall{args_lost: true} = pending_call, %Config{}, _context) do
    {pending_call,
     {:error,
      %{
        type: :args_lost,
        message:
          "The arguments for this call to '#{pending_call.name}' did not arrive intact " <>
            "(the stream was interrupted mid-call). Issue the tool call again."
      }, []}, 1, 0}
  end

  defp execute_tool_with_retries(%PendingToolCall{} = pending_call, %Config{} = config, context) do
    module = Map.get(config.tools, pending_call.name)

    case is_atom(module) and function_exported?(module, :name, 0) and function_exported?(module, :run, 2) do
      true ->
        do_execute_tool_with_retries(pending_call, module, config, context, 1)

      _ ->
        {pending_call, {:error, %{type: :unknown_tool, message: "Tool '#{pending_call.name}' not found"}, []}, 1, 0}
    end
  end

  defp do_execute_tool_with_retries(%PendingToolCall{} = pending_call, module, %Config{} = config, context, attempt) do
    start_ms = System.monotonic_time(:millisecond)
    timeout_ms = normalize_timeout(config.tool_exec[:timeout_ms])
    telemetry_metadata = %{call_id: pending_call.id, tool_call_id: pending_call.id}

    result =
      safe_execute_module(module, pending_call.arguments, context,
        timeout: timeout_ms,
        max_retries: 0,
        telemetry_metadata: telemetry_metadata
      )

    duration_ms = max(System.monotonic_time(:millisecond) - start_ms, 0)
    max_retries = normalize_retry_count(config.tool_exec[:max_retries])
    backoff_ms = normalize_backoff(config.tool_exec[:retry_backoff_ms])

    case Error.retryable?(result) and attempt <= max_retries do
      true ->
        case backoff_ms > 0 do
          true -> Process.sleep(backoff_ms)
          _ -> :ok
        end

        do_execute_tool_with_retries(pending_call, module, config, context, attempt + 1)

      _ ->
        {pending_call, result, attempt, duration_ms}
    end
  end

  defp finalize(%State{} = state, owner, ref, %Config{} = config) do
    {state, _token} = emit_checkpoint(state, owner, ref, config, :terminal)
    send(owner, {:react_runner, ref, :done})
    state
  end

  defp complete_run(%State{} = state, owner, ref, %Config{} = config, termination_reason) do
    with {:ok, state} <- finalize_output(state, owner, ref, config) do
      {state, _} =
        emit_event(state, owner, ref, :request_completed, %{
          result: state.result,
          termination_reason: termination_reason,
          usage: state.usage,
          output: state.output
        })

      {:ok, state}
    end
  end

  defp finalize_output(%State{} = state, _owner, _ref, %Config{output: nil}), do: {:ok, state}

  defp finalize_output(%State{} = state, owner, ref, %Config{output: %Output{} = output} = config) do
    context = output_context(state, config)
    {state, _} = emit_output_event(state, owner, ref, :output_started, output, :started, state.result, attempt: 0)

    case Output.parse(output, state.result) do
      {:ok, parsed} ->
        meta = Output.meta(output, :validated, state.result, attempt: 0)

        {state, _} =
          emit_output_event(state, owner, ref, :output_validated, output, :validated, parsed, attempt: 0)

        {:ok, state |> State.put_result(parsed) |> State.put_output(meta)}

      {:error, reason} ->
        repair_or_fail_output(state, owner, ref, output, context, state.result, reason, 1)
    end
  end

  defp repair_or_fail_output(
         state,
         owner,
         ref,
         %Output{on_validation_error: :repair, retries: retries} = output,
         context,
         raw,
         reason,
         attempt
       )
       when attempt <= retries do
    {state, _} =
      emit_output_event(state, owner, ref, :output_repair, output, :repair, raw,
        attempt: attempt,
        validation_error: reason
      )

    case Output.repair(output, raw, reason, context) do
      {:ok, parsed} ->
        meta = Output.meta(output, :repaired, raw, attempt: attempt, validation_error: reason)

        {state, _} =
          emit_output_event(state, owner, ref, :output_validated, output, :validated, parsed, attempt: attempt)

        {:ok, state |> State.put_result(parsed) |> State.put_output(meta)}

      {:error, repair_reason} ->
        repair_or_fail_output(state, owner, ref, output, context, raw, repair_reason, attempt + 1)
    end
  end

  defp repair_or_fail_output(state, owner, ref, %Output{} = output, _context, raw, reason, attempt) do
    meta = Output.meta(output, :error, raw, attempt: max(attempt - 1, 0), error: reason)

    {state, _} =
      emit_output_event(state, owner, ref, :output_failed, output, :error, raw,
        attempt: max(attempt - 1, 0),
        error: reason
      )

    {:error, State.put_output(state, meta), reason, :output_validation}
  end

  defp emit_output_event(%State{} = state, owner, ref, kind, %Output{} = output, status, raw, opts) do
    data =
      output
      |> Output.meta(status, raw, opts)
      |> Map.put(:schema_summary, output_schema_summary(output))

    emit_event(state, owner, ref, kind, data)
  end

  defp output_context(%State{} = state, %Config{} = config) do
    %{
      model: config.model,
      llm_opts: Config.llm_opts(config),
      user_message: latest_query(state),
      request_id: state.request_id,
      run_id: state.run_id
    }
  end

  defp output_schema_summary(%Output{} = output) do
    schema = Output.json_schema(output)

    %{
      schema_kind: output.schema_kind,
      required: Map.get(schema, "required", Map.get(schema, :required, [])),
      properties:
        schema
        |> Map.get("properties", Map.get(schema, :properties, %{}))
        |> Map.keys()
    }
  rescue
    _ -> %{schema_kind: output.schema_kind}
  end

  defp emit_checkpoint(%State{} = state, owner, ref, %Config{} = config, reason)
       when reason in [:after_llm, :after_tools, :terminal] do
    token = Token.issue(state, config)

    emit_event(state, owner, ref, :checkpoint, %{
      token: token,
      reason: reason
    })
    |> then(fn {updated, _event} -> {updated, token} end)
  end

  defp emit_event(%State{} = state, owner, ref, kind, data, extra \\ %{}) do
    {state, seq} = State.bump_seq(state)

    event =
      Event.new(%{
        seq: seq,
        run_id: state.run_id,
        request_id: state.request_id,
        iteration: state.iteration,
        kind: kind,
        llm_call_id: fetch_extra(extra, :llm_call_id, state.llm_call_id),
        tool_call_id: fetch_extra(extra, :tool_call_id),
        tool_name: fetch_extra(extra, :tool_name),
        data: data
      })

    send(owner, {:react_runner, ref, :event, event})
    {state, event}
  end

  defp next_event(_owner, %{done?: true} = state), do: {:halt, state}

  defp next_event(_owner, %{done?: false, down?: true, ref: ref} = state) do
    receive do
      {:react_stream_cancel, _reason} ->
        next_event(nil, state)

      {:react_runner, ^ref, :stream_control, control} ->
        next_event(nil, apply_stream_control(state, control))

      {:react_runner, ^ref, :progress} ->
        next_event(nil, state)

      {:react_runner, ^ref, :event, event} ->
        {[event], state}

      {:react_runner, ^ref, :done} ->
        {:halt, %{state | done?: true}}
    after
      0 ->
        {:halt, %{state | done?: true}}
    end
  end

  defp next_event(_owner, %{ref: ref} = state) do
    receive_timeout_ms = Map.get(state, :receive_timeout_ms, 30_000)

    receive do
      {:react_stream_cancel, reason} ->
        next_event(nil, request_stream_cancel(state, reason))

      {:react_runner, ^ref, :stream_control, control} ->
        next_event(nil, apply_stream_control(state, control))

      {:react_runner, ^ref, :progress} ->
        next_event(nil, state)

      {:react_runner, ^ref, :event, event} ->
        {[event], state}

      {:react_runner, ^ref, :done} ->
        {:halt, %{state | done?: true}}

      {:DOWN, monitor_ref, :process, _pid, _reason} when monitor_ref == state.monitor_ref ->
        next_event(nil, %{state | down?: true})
    after
      receive_timeout_ms ->
        {:halt, %{state | done?: true}}
    end
  end

  defp notify_progress(owner, ref) do
    send(owner, {:react_runner, ref, :progress})
    :ok
  end

  defp cleanup(_owner, %{pid: pid} = state) when is_pid(pid) do
    state = request_stream_cancel(state, :stream_halted)
    await_runner_shutdown(state)
    :ok
  end

  defp start_task(fun, opts) do
    case Keyword.get(opts, :task_supervisor) do
      task_sup when is_pid(task_sup) ->
        Task.Supervisor.start_child(task_sup, fun)

      task_sup when is_atom(task_sup) and not is_nil(task_sup) ->
        case Process.whereis(task_sup) do
          pid when is_pid(pid) ->
            Task.Supervisor.start_child(task_sup, fun)

          _ ->
            Task.start(fun)
        end

      _ ->
        Task.start(fun)
    end
  end

  defp request_id_opts(opts) do
    opts
    |> Keyword.take([:request_id, :run_id])
  end

  defp latest_query(%State{} = state) do
    case AIContext.last_entry(state.context) do
      %{role: :user, content: content} when is_binary(content) -> content
      %{role: :user, content: content} when is_list(content) -> Query.summarize(content)
      _ -> ""
    end
  end

  defp append_query(%State{} = state, query) when is_binary(query) or is_list(query) do
    %{state | context: AIContext.append_user(state.context, query), status: :running, updated_at_ms: now_ms()}
  end

  defp maybe_thinking_opt(nil), do: []
  defp maybe_thinking_opt(""), do: []
  defp maybe_thinking_opt(thinking), do: [thinking: thinking]

  defp assistant_context_opts(%Turn{} = turn) do
    turn.thinking_content
    |> maybe_thinking_opt()
    |> maybe_put_assistant_context_opt(:reasoning_details, turn.reasoning_details)
  end

  defp maybe_put_assistant_context_opt(opts, _key, nil), do: opts
  defp maybe_put_assistant_context_opt(opts, key, value), do: Keyword.put(opts, key, value)

  # Full content parts ride the assistant entry only when the turn produced
  # provider-native blocks (server tools): those must be replayed verbatim on
  # subsequent requests (encrypted_content), while every other turn keeps the
  # plain text/tool_calls projection.
  defp provider_content_parts(%Turn{content_parts: parts}) when is_list(parts) do
    if Enum.any?(parts, &match?(%{type: :provider_block}, &1)), do: parts
  end

  defp provider_content_parts(_), do: nil

  defp apply_stream_accumulator(%Turn{} = turn, %State{} = state) do
    turn
    |> maybe_put_accumulated_text(state.streaming_text)
    |> maybe_put_accumulated_thinking(state.streaming_thinking)
  end

  defp apply_stream_accumulator(%Turn{} = turn, _state), do: turn

  defp maybe_put_accumulated_text(%Turn{type: :final_answer, text: text} = turn, accumulated)
       when text in [nil, ""] and is_binary(accumulated) and accumulated != "" do
    %{turn | text: accumulated}
  end

  defp maybe_put_accumulated_text(turn, _accumulated), do: turn

  defp maybe_put_accumulated_thinking(%Turn{thinking_content: thinking} = turn, accumulated)
       when thinking in [nil, ""] and is_binary(accumulated) and accumulated != "" do
    %{turn | thinking_content: accumulated}
  end

  defp maybe_put_accumulated_thinking(turn, _accumulated), do: turn

  defp maybe_note_owner_signal(true, _owner, _ref, _last_owner_signal_ms, _interval_ms), do: monotonic_ms()

  defp maybe_note_owner_signal(false, owner, ref, last_owner_signal_ms, interval_ms) do
    current_ms = monotonic_ms()

    if current_ms - last_owner_signal_ms >= interval_ms do
      notify_progress(owner, ref)
      current_ms
    else
      last_owner_signal_ms
    end
  end

  defp progress_interval_ms(%Config{} = config) do
    max(1, div(Config.stream_timeout(config), 2))
  end

  defp visible_chunk?(%ReqLLM.StreamChunk{type: :content, text: text}, trace_cfg), do: delta_captured?(text, trace_cfg)
  defp visible_chunk?(%ReqLLM.StreamChunk{type: :thinking, text: text}, trace_cfg), do: delta_captured?(text, trace_cfg)
  defp visible_chunk?(%ReqLLM.StreamChunk{type: :tool_call}, trace_cfg), do: trace_cfg[:capture_deltas?] == true
  defp visible_chunk?(_chunk, _trace_cfg), do: false

  defp delta_captured?(text, trace_cfg), do: trace_cfg[:capture_deltas?] == true and is_binary(text) and text != ""

  defp stream_process_opts(owner, ref, trace_cfg, state_key, heartbeat_interval_ms, model) do
    chunks_key = stream_chunks_key(ref)

    []
    |> Keyword.put(:on_chunk, fn chunk ->
      capture_stream_chunk(chunks_key, chunk)
      note_stream_chunk_activity(chunk, state_key, owner, ref, trace_cfg, heartbeat_interval_ms)
      maybe_emit_server_tool_started(chunk, state_key, owner, ref, model)
    end)
    |> maybe_put_stream_callback(trace_cfg, :on_result, fn text ->
      emit_stream_delta(state_key, owner, ref, :content, text, model)
    end)
    |> maybe_put_stream_callback(trace_cfg, :on_thinking, fn text ->
      emit_stream_delta(state_key, owner, ref, :thinking, text, model)
    end)
    |> maybe_put_stream_callback(trace_cfg, :on_tool_call, fn chunk ->
      emit_stream_delta(state_key, owner, ref, :tool_call, chunk.name || "", model)
    end)
  end

  defp maybe_put_stream_callback(opts, trace_cfg, callback_key, callback_fun) do
    case trace_cfg[:capture_deltas?] do
      true -> Keyword.put(opts, callback_key, callback_fun)
      _ -> opts
    end
  end

  # A provider-executed (server) tool started inside the API. Surface it the
  # same way a client tool call's streamed name is surfaced, so downstream
  # consumers can show activity while the tool runs server-side.
  defp maybe_emit_server_tool_started(
         %ReqLLM.StreamChunk{type: :meta, metadata: %{server_tool_started: name}},
         state_key,
         owner,
         ref,
         model
       )
       when is_binary(name) do
    emit_stream_delta(state_key, owner, ref, :tool_call, name, model)
  end

  defp maybe_emit_server_tool_started(_chunk, _state_key, _owner, _ref, _model), do: :ok

  defp emit_stream_delta(_state_key, _owner, _ref, _chunk_type, text, _model) when text in [nil, ""], do: :ok

  defp emit_stream_delta(state_key, owner, ref, chunk_type, text, model) do
    update_stream_state(state_key, fn
      %State{} = current_state ->
        current_state = append_stream_delta(current_state, chunk_type, text)

        {next_state, _} =
          emit_event(current_state, owner, ref, :llm_delta, %{
            chunk_type: chunk_type,
            delta: text,
            model: Jido.AI.model_label(model)
          })

        next_state

      other ->
        other
    end)

    :ok
  end

  defp append_stream_delta(%State{} = state, :content, text) when is_binary(text) do
    %{state | streaming_text: state.streaming_text <> text, updated_at_ms: now_ms()}
  end

  defp append_stream_delta(%State{} = state, :thinking, text) when is_binary(text) do
    %{state | streaming_thinking: state.streaming_thinking <> text, updated_at_ms: now_ms()}
  end

  defp append_stream_delta(%State{} = state, _chunk_type, _text), do: state

  defp stream_state_key(ref), do: {__MODULE__, :stream_state, ref}
  defp stream_signal_key(ref), do: {__MODULE__, :stream_signal, ref}
  defp stream_activity_key(ref), do: {__MODULE__, :stream_activity, ref}
  defp stream_chunks_key(ref), do: {__MODULE__, :stream_chunks, ref}

  defp note_stream_chunk_activity(chunk, state_key, owner, ref, trace_cfg, heartbeat_interval_ms) do
    case current_stream_state(state_key, nil) do
      %State{} = current_state -> check_cancel!(current_state, ref)
      _ -> :ok
    end

    visible? = visible_chunk?(chunk, trace_cfg)

    last_owner_signal_ms = Process.get(stream_signal_key(ref), monotonic_ms())

    last_owner_signal_ms =
      maybe_note_owner_signal(
        visible?,
        owner,
        ref,
        last_owner_signal_ms,
        heartbeat_interval_ms
      )

    Process.put(stream_signal_key(ref), last_owner_signal_ms)
    maybe_emit_stream_activity(visible?, state_key, owner, ref)
    :ok
  end

  # How often a silent-but-working stream announces itself as an event.
  # `notify_progress/2` already keeps THIS process's receive alive, but it is a
  # bare message — it never becomes a stream element, so a consumer watching the
  # event stream for inactivity (see `Request.Stream.events/2`) cannot see it and
  # will halt a turn that is making progress. Chunks a server tool absorbs
  # (streaming tool input, provider result blocks, keepalives/pings) are exactly
  # that case: minutes of real work, zero events. This interval is deliberately
  # much shorter than any consumer timeout so it stays useful if one is tightened.
  @stream_activity_interval_ms 15_000

  # A visible chunk becomes an `:llm_delta` on its own, so it only restarts the
  # clock; silence after it is what needs announcing.
  defp maybe_emit_stream_activity(true, _state_key, _owner, ref) do
    Process.put(stream_activity_key(ref), monotonic_ms())
    :ok
  end

  defp maybe_emit_stream_activity(false, state_key, owner, ref) do
    current_ms = monotonic_ms()
    last_ms = Process.get(stream_activity_key(ref))

    if is_nil(last_ms) or current_ms - last_ms >= @stream_activity_interval_ms do
      emit_stream_activity(state_key, owner, ref)
      Process.put(stream_activity_key(ref), current_ms)
    end

    :ok
  end

  defp emit_stream_activity(state_key, owner, ref) do
    update_stream_state(state_key, fn
      %State{} = current_state ->
        {next_state, _event} = emit_event(current_state, owner, ref, :stream_activity, %{})
        next_state

      other ->
        other
    end)

    :ok
  end

  defp current_stream_state(state_key, default) do
    Process.get(state_key, default)
  end

  defp update_stream_state(state_key, update_fun) when is_function(update_fun, 1) do
    current_state = Process.get(state_key)
    next_state = update_fun.(current_state)
    Process.put(state_key, next_state)
    next_state
  end

  defp maybe_redact_args(arguments, %Config{} = config) do
    case config.observability[:redact_tool_args?] do
      true -> Jido.AI.Observe.sanitize_sensitive(arguments)
      _ -> arguments
    end
  end

  defp check_cancel!(%State{} = state, ref) do
    receive do
      {:react_cancel, ^ref, reason} -> throw({:cancelled, state, reason})
    after
      0 -> :ok
    end
  end

  defp maybe_continue_after_final_answer(%State{} = state, owner, ref, %Config{} = config) do
    case seal_pending_input_server_if_empty(config) do
      :pending ->
        case drain_pending_input(state, owner, ref, config) do
          {:ok, state} ->
            state =
              state
              |> State.put_status(:running)
              |> State.put_result(nil)
              |> State.inc_iteration()

            {:continue, state}

          {:error, state, reason} ->
            {:error, state, {:pending_input_server, reason}, :runtime}
        end

      :sealed ->
        case complete_run(state, owner, ref, config, :final_answer) do
          {:ok, state} -> {:complete, state}
          {:error, state, reason, error_type} -> {:error, state, reason, error_type}
        end

      {:error, reason} ->
        {:error, state, {:pending_input_server, reason}, :runtime}
    end
  end

  defp drain_pending_input(%State{} = state, _owner, _ref, %Config{pending_input_server: nil}),
    do: {:ok, state}

  defp drain_pending_input(%State{} = state, owner, ref, %Config{pending_input_server: server}) do
    case PendingInputServer.drain_result(server) do
      {:ok, items} ->
        next_state =
          Enum.reduce(items, state, fn item, acc ->
            refs = normalize_optional_refs(item[:refs])
            next_context = AIContext.append_user(acc.context, item.content, refs: refs)
            next_state = %{acc | context: next_context}

            {next_state, _} =
              emit_event(next_state, owner, ref, :input_injected, %{
                input_id: item.id,
                content: item.content,
                source: item.source,
                refs: refs,
                at_ms: item.at_ms
              })

            next_state
          end)

        {:ok, next_state}

      {:error, reason} ->
        {:error, state, reason}
    end
  end

  # Returns :ok for tool-call turns and accepted terminal responses.
  # Rejects blank terminal responses when the provider reported a non-success
  # finish reason so we do not emit a phantom assistant turn or checkpoint it.
  defp validate_terminal_response(%Turn{} = turn) do
    cond do
      Turn.needs_tools?(turn) ->
        :ok

      turn.text != "" ->
        :ok

      invalid_blank_terminal_finish_reason?(turn.finish_reason) ->
        {:error, {:incomplete_response, turn.finish_reason}}

      true ->
        :ok
    end
  end

  defp invalid_blank_terminal_finish_reason?(reason) when reason in [nil, :stop, :tool_calls], do: false
  defp invalid_blank_terminal_finish_reason?(_reason), do: true

  defp fail_run(%State{} = state, owner, ref, %Config{} = config, reason, error_type) do
    seal_pending_input_server(config)

    state
    |> State.put_status(:failed)
    |> State.put_error(reason)
    |> then(fn failed ->
      {failed, _} =
        emit_event(failed, owner, ref, :request_failed, %{
          error: reason,
          error_type: error_type,
          usage: failed.usage
        })

      failed
    end)
  end

  defp seal_pending_input_server(%Config{pending_input_server: nil}), do: :ok
  defp seal_pending_input_server(%Config{pending_input_server: server}), do: PendingInputServer.seal(server)

  defp seal_pending_input_server_if_empty(%Config{pending_input_server: nil}), do: :sealed

  defp seal_pending_input_server_if_empty(%Config{pending_input_server: server}) do
    PendingInputServer.seal_if_empty(server)
  end

  defp normalize_optional_refs(%{} = refs), do: refs
  defp normalize_optional_refs(_), do: nil

  defp fetch_extra(extra, key, default \\ nil)

  defp fetch_extra(extra, key, default) when is_map(extra) do
    Map.get(extra, key, default)
  end

  defp fetch_extra(extra, key, default) when is_list(extra) do
    Keyword.get(extra, key, default)
  end

  defp request_stream_cancel(%{cancel_sent?: true} = state, _reason), do: state

  defp request_stream_cancel(%{pid: pid, ref: ref} = state, reason) when is_pid(pid) do
    state =
      state
      |> maybe_capture_stream_control()
      |> invoke_stream_cancel()

    case Process.alive?(pid) do
      true -> send(pid, {:react_cancel, ref, reason})
      _ -> :ok
    end

    Map.put(state, :cancel_sent?, true)
  end

  defp request_stream_cancel(state, _reason) do
    state
    |> maybe_capture_stream_control()
    |> invoke_stream_cancel()
    |> Map.put(:cancel_sent?, true)
  end

  defp announce_stream_control(owner, ref, stream_response) do
    case stream_cancel_fun(stream_response) do
      cancel_fun when is_function(cancel_fun, 0) ->
        send(owner, {:react_runner, ref, :stream_control, %{cancel: cancel_fun}})

      _ ->
        :ok
    end

    :ok
  end

  defp apply_stream_control(state, %{cancel: cancel_fun}) when is_function(cancel_fun, 0) do
    %{state | stream_cancel: cancel_fun}
  end

  defp apply_stream_control(state, _control), do: state

  defp maybe_capture_stream_control(%{stream_cancel: cancel_fun} = state) when is_function(cancel_fun, 0), do: state

  defp maybe_capture_stream_control(%{ref: ref} = state) do
    receive do
      {:react_runner, ^ref, :stream_control, control} ->
        apply_stream_control(state, control)
    after
      @stream_control_wait_ms ->
        state
    end
  end

  defp invoke_stream_cancel(%{stream_cancelled?: true} = state), do: state

  defp invoke_stream_cancel(%{stream_cancel: cancel_fun} = state) when is_function(cancel_fun, 0) do
    try do
      _ = cancel_fun.()
    catch
      _, _ -> :ok
    end

    %{state | stream_cancelled?: true}
  end

  defp invoke_stream_cancel(state), do: state

  defp await_runner_shutdown(%{pid: pid, monitor_ref: monitor_ref}) when is_pid(pid) do
    case Process.alive?(pid) do
      true ->
        receive do
          {:DOWN, ^monitor_ref, :process, ^pid, _reason} ->
            :ok
        after
          @cleanup_wait_ms ->
            Process.exit(pid, :kill)
            :ok
        end

      _ ->
        :ok
    end
  end

  defp await_runner_shutdown(_state), do: :ok

  defp stream_cancel_fun(%{cancel: cancel_fun}) when is_function(cancel_fun, 0), do: cancel_fun
  defp stream_cancel_fun(_stream_response), do: nil

  defp safe_execute_module(module, params, context, opts) do
    Turn.execute_module(module, params, context, opts)
  rescue
    error ->
      {:error, %{type: :exception, error: Exception.message(error), exception_type: error.__struct__}, []}
  catch
    kind, reason ->
      {:error, %{type: :caught, kind: kind, error: inspect(reason)}, []}
  end

  defp normalize_timeout(value) when is_integer(value) and value > 0, do: value
  defp normalize_timeout(_), do: 15_000

  defp normalize_retry_count(value) when is_integer(value) and value >= 0, do: value
  defp normalize_retry_count(_), do: 0

  defp normalize_backoff(value) when is_integer(value) and value >= 0, do: value
  defp normalize_backoff(_), do: 0

  defp now_ms, do: System.system_time(:millisecond)
  defp monotonic_ms, do: System.monotonic_time(:millisecond)

  # Tool call maps may arrive with atom or string keys depending on the
  # provider adapter, so we check both.
  defp tool_call_signature(tool_calls) when is_list(tool_calls) do
    tool_calls
    |> Enum.map(fn tc ->
      name = Map.get(tc, :name, "")
      args = Map.get(tc, :arguments, "")
      "#{name}:#{inspect(args)}"
    end)
    |> Enum.sort()
    |> Enum.join("|")
  end
end
